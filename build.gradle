plugins {
    id 'java-library'
    id 'application'
    id 'eclipse'
    id 'maven-publish'
    id 'org.jreleaser' version '1.22.0'
}

def mlGhBase                   = 'github.com/maamissiniva/distance-field-font'
def mlArName                   = 'maamissiniva-distance-field-font'
def mlDesc                     = 'Distance field font generation.'
def mlYear                     = '2025'

eclipse.project.name           = '_github_maamissiniva_distance-field-font'
java.toolchain.languageVersion = JavaLanguageVersion.of(8)
jar.archiveBaseName            = mlArName
group                          = 'io.github.maamissiniva'
version                        = '0.1.0'

application.mainClass = 'maamissiniva.distancefieldfont.DistanceFieldFont'

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    implementation 'io.github.maamissiniva:maamissiniva-bitmap-font-descriptor:0.1.0'
    implementation 'com.fasterxml.jackson.core:jackson-databind:[2.13,)'
    implementation 'org.slf4j:slf4j-api:1.7.30'
    implementation 'ch.qos.logback:logback-classic:1.2.3'

    testImplementation 'junit:junit:4.13'
}

//test {
//    testLogging {
//        showStandardStreams = true
//    }
//}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId    = group
            artifactId = mlArName

            from components.java

            pom {
                name          = mlArName
                description   = mlDesc
                url           = "https://${mlGhBase}"
                inceptionYear = mlYear
                licenses {
                    license {
                        name = 'LGPL-2.1'
                        url  = 'https://www.gnu.org/licenses/old-licenses/lgpl-2.1.en.html'
                    }
                }
                developers {
                    developer {
                        id    = 'maamissiniva'
                        name  = 'Maamissiniva'
                        email = 'maamissiniva@gmail.com'
                    }
                }
                scm {
                    connection          = "scm:git:https://${mlGhBase}.git"
                    developerConnection = "scm:git:ssh://${mlGhBase}.git"
                    url                 = "https://${mlGhBase}"
                }
            }
        }
    }

    repositories {
        maven {
            url = layout.buildDirectory.dir('staging-deploy')
        }
    }
}

jreleaser {
    signing {
        pgp {
            active = 'ALWAYS'
            armored = true
        }
    }
    deploy {
        maven {
            mavenCentral {
                'release-deploy' {
                    active = 'RELEASE'
                    url    = 'https://central.sonatype.com/api/v1/publisher'
                    stagingRepository('build/staging-deploy')
                }
            }
            nexus2 {
                'snapshot-deploy' {
                    active                 = 'SNAPSHOT'
                    snapshotUrl            = 'https://central.sonatype.com/repository/maven-snapshots/'
                    applyMavenCentralRules = true
                    snapshotSupported      = true
                    closeRepository        = true
                    releaseRepository      = true
                    stagingRepository('build/staging-deploy')
                }
            }
        }
    }
}

